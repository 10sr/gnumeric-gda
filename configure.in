AC_INIT(src/gnumeric.h)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnumeric,0.56)
AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)

# Make --disable-static the default
AC_DISABLE_STATIC

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_YACC
AM_PROG_LEX
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

GNOME_INIT

## this should come after `AC_PROG_CC'
GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK

dnl
dnl alloca tests
dnl;
AC_FUNC_ALLOCA
if test $ac_cv_header_alloca_h = yes; then
        GNUMERIC_HAVE_ALLOCA_H=1
else
        GNUMERIC_HAVE_ALLOCA_H=0
fi
AC_SUBST(GNUMERIC_HAVE_ALLOCA_H)

AC_SUBST(QTTHREADS_LIB)
AC_SUBST(TERMCAP_LIB)
AC_SUBST(READLINE_LIB)
AC_SUBST(GUILE_LIBS)
AC_SUBST(GUILE_INCS)
AC_ARG_WITH(guile,[--with-guile   Include Guile support],[GNOME_CHECK_GUILE])

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl **************************************************
dnl * internationalization support
dnl **************************************************
dnl 
dnl
dnl Check doc/translating.sgml for a description of how to translate
dnl and why we have so many translations.
dnl
ALL_LINGUAS="cs da de en_GB el es es_DO es_GT es_HN es_MX es_PA es_PE es_SV et fi fr ga gl hr hu it ja ko nl no pl pt pt_BR ru sk sv uk zh_CN.GB2312 zh_TW.Big5"
AM_GNU_GETTEXT
# AM_GNOME_GETTEXT above substs $DATADIRNAME
# this is the directory where the *.{mo,gmo} files are installed
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)


dnl
dnl On Solaris finite() needs ieeefp.h
dnl
AC_CHECK_HEADERS(ieeefp.h)

dnl Check for some functions
AC_CHECK_FUNCS(random drand48)

dnl **************************************************
dnl * ORBit support
dnl **************************************************
GNOMEGNORBA_LIBS="$GNOMEGNORBA_LIBS"
AC_SUBST(GNOMEGNORBA_LIBS)

dnl ******************************
dnl gnome-xml checking
dnl ******************************
AC_MSG_CHECKING(for libxml (aka gnome-xml) libraries 1.8.5 <= version < 2.0.0)
if xml-config --libs print > /dev/null 2>&1; then 
    vers=`xml-config --version | sed -e "s/libxml //" | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
    if test "$vers" -ge 1008005; then
	if test "$vers" -lt 2000000; then
	    AC_MSG_RESULT(found)
	else
	    AC_MSG_ERROR(This version of Gnumeric does not support libxml (aka gnome-xml) 2.x.x yet.)
	fi
    else
        AC_MSG_ERROR(You need at least libxml (aka gnome-xml) 1.8.5 for this version of Gnumeric.)
    fi
else
    AC_MSG_ERROR(Did not find libxml (aka gnome-xml) installed)
fi


dnl **************************************************
dnl * Check for Perl
dnl **************************************************
dnl
AC_CHECK_PROG(perl_val, perl, true, false)
if $perl_val; then
  AC_MSG_CHECKING(for perl ExtUtils::Embed module)
  perl -e 'eval { require ExtUtils::Embed }; if ($@) { exit(1); } else { exit(0); }'
  if test "x$?" = "x0"; then
    AC_MSG_RESULT(yes)

    dnl Use ExtUtils::Embed to figure out the other options.
    PERL_CCCDLFLAGS=`perl -MConfig -e 'print $Config{cccdlflags},"\n";'`
    PERL_LDDLFLAGS=`perl -MConfig -e 'print $Config{lddlflags},"\n";'`
    PERL_CC=`perl -MConfig -e 'print $Config{cc},"\n";'`
    PERL_LD=`perl -MConfig -e 'print $Config{ld},"\n";'`
    PERL_CCOPTS=`perl -MExtUtils::Embed -e ccopts`
    PERL_LDOPTS=`perl -MExtUtils::Embed -e ldopts`
    AC_SUBST(PERL_CCCDLFLAGS)
    AC_SUBST(PERL_LDDLFLAGS)
    AC_SUBST(PERL_CC)
    AC_SUBST(PERL_LD)
    AC_SUBST(PERL_CCOPTS)
    AC_SUBST(PERL_LDOPTS)
  else
    AC_MSG_RESULT(no)
    perl_val=false
  fi
fi

dnl Disable perl until we have an active maintainer
AM_CONDITIONAL(WITH_PERL, false)

dnl **************************************************
dnl * Check for Python
dnl **************************************************
AC_CHECK_PROG(python_val, python, true, false)
if $python_val; then
	  PY_PREFIX=`python -c 'import sys ; print sys.prefix'`
	  PY_EXEC_PREFIX=`python -c 'import sys ; print sys.exec_prefix'`
	  changequote(<<, >>)dnl
	  PY_VERSION=`python -c 'import sys ; print sys.version[0:3]'`
	  changequote([, ])dnl
	  if test -f $PY_PREFIX/include/python$PY_VERSION/Python.h; then
		  PY_LIBS="python$PY_VERSION"
		  PY_LIB_LOC="-L$PY_EXEC_PREFIX/lib/python$PY_VERSION/config"
		  PY_CFLAGS="-I$PY_PREFIX/include/python$PY_VERSION"
		  PY_MAKEFILE="$PY_EXEC_PREFIX/lib/python$PY_VERSION/config/Makefile"
		  PY_LOCALMODLIBS=`sed -n -e 's/^LOCALMODLIBS=\(.*\)/\1/p' $PY_MAKEFILE`
		  PY_BASEMODLIBS=`sed -n -e 's/^BASEMODLIBS=\(.*\)/\1/p' $PY_MAKEFILE`
		  PY_OTHER_LIBS=`sed -n -e 's/^LIBS=\(.*\)/\1/p' $PY_MAKEFILE`
		  PY_EXTRA_LIBS="$PY_LOCALMODLIBS $PY_BASEMODLIBS $PY_OTHER_LIBS"
		  AC_SUBST(PY_LIBS)
		  AC_SUBST(PY_LIB_LOC)
		  AC_SUBST(PY_CFLAGS)
		  AC_SUBST(PY_EXTRA_LIBS)
	  else
		  python_val=false
	  fi
fi
AM_CONDITIONAL(WITH_PYTHON, $python_val)


dnl Check for libole2
AC_MSG_CHECKING(for libole2 >= 0.1.4)
vers_ok="";
vers=`gnome-config --modversion libole2 | grep "libole2-" | \
	sed -e "s/libole2-//" | \
	awk 'BEGIN { FS = "."; } { printf "%d", $1 * 10000 + $2 * 100 + $3 ;}'`
if test 0"$vers"; then
       if test "$vers" -ge 104; then
               vers_ok="yes";
       fi
fi

if test "$vers_ok"; then
	AC_MSG_RESULT(found)
else
	AC_MSG_ERROR(not found


this version of gnumeric requires an external
libole2 library the latest version can be obtained from
ftp://ftp.gnome.org/pub/GNOME/unstable/sources/libole2/

)
fi

dnl ******************************
dnl Check for Bonobo
dnl ******************************
try_bonobo=true
bonobo=
bonobo_msg=no
using_oaf=no
bonobo_suffix=
have_bonobo=false
AC_ARG_WITH(bonobo,
	[--{with,without}-bonobo   Compile with Bonobo support or without it],
	if test x$withval = xno; then
		try_bonobo=false
	fi
)

gnumeric_executable=gnumeric
AC_SUBST(gnumeric_executable)
if $try_bonobo; then
	AC_MSG_CHECKING(for Bonobo == 0.19)
	vers=`gnome-config --modversion bonobo | sed -e "s/bonobo-//" | \
		awk 'BEGIN { FS = "."; } { printf "%d", $1 * 1000 + $2;}'`
	if test "$vers" -eq 19; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_BONOBO)
		have_bonobo=true
		bonobo=bonobox_print
		bonobo_msg=yes
		bonobo_suffix=-bonobo
	        gnumeric_executable=gnumeric-bonobo

		dnl Check if Bonobo is OAFized

		AC_MSG_CHECKING(if Bonobo uses OAF)
		if ( gnome-config --libs bonobo | grep oaf ) > /dev/null 2>&1 ; then
			using_oaf="yes"
			AC_DEFINE(USING_OAF)
		fi

		AC_MSG_RESULT("$using_oaf")

		AM_CONDITIONAL(USING_OAF, test x"using_oaf" = "xyes")
	else
		AC_MSG_RESULT(not found)
	fi
fi
gnumeric_bonobo_enabled=$bonobo_msg
AM_CONDITIONAL(BONOBO, $have_bonobo)


dnl **************************************************
dnl * Config defaults (Must be after bonobo)
dnl **************************************************
gnumeric_prefix="${prefix}"
gnumeric_exec_prefix="${exec_prefix}"
gnumeric_version="${VERSION}${bonobo_suffix}"
gnumeric_datadir="${datadir}/gnumeric/${gnumeric_version}/"
gnumeric_libdir="${libdir}/gnumeric/${gnumeric_version}/"
gnumeric_plugindir="${gnumeric_libdir}plugins/"
gnumeric_gladedir="${gnumeric_datadir}glade/"
gnumeric_autoformatdir="${gnumeric_datadir}autoformat-templates/"

dnl Export to config.h
AC_DEFINE_UNQUOTED(GNUMERIC_VERSION, "$gnumeric_version")

AC_SUBST(gnumeric_prefix)
AC_SUBST(gnumeric_exec_prefix)
AC_SUBST(gnumeric_datadir)
AC_SUBST(gnumeric_version)
AC_SUBST(gnumeric_libdir)
AC_SUBST(gnumeric_plugindir)
AC_SUBST(gnumeric_gladedir)
AC_SUBST(gnumeric_autoformatdir)
AC_SUBST(gnumeric_bonobo_enabled)

dnl ******************************
dnl Check for GB
dnl ******************************
try_gb=true
gb=
gb_msg=no
have_gb=false
GB_CFLAGS=
GB_LIBS=
AC_ARG_WITH(gb,
	[--{with,without}-gb       Compile with Gb support or without it],
	if test x$withval = xno; then
		try_gb=false
	fi
)

if $try_gb; then
	AC_MSG_CHECKING(for Gb >= 0.0.10)
	if gnome-config --libs gb > /dev/null 2>&1; then
		vers=`gnome-config --modversion gb | awk 'BEGIN { FS = "."; } { print $1 * 10000 + $2 * 100 + $3; }'`
		if test "$vers" -ge 000010; then
			gb_ok=true
		else
		    	gb_ok=false
		fi
	else
		gb_ok=false
	fi
	
	if $gb_ok; then
		AC_MSG_RESULT(found)
		AC_DEFINE(ENABLE_GB)
		GB_LIBS=`gnome-config --libs gb`
		GB_CFLAGS=`gnome-config --cflags gb`
		have_gb=true
		gb=gb
		gb_msg=yes
	else
		AC_MSG_RESULT(not found)
	fi
fi
AM_CONDITIONAL(WITH_GB, $have_gb)
AC_SUBST(GB_CFLAGS)
AC_SUBST(GB_LIBS)

dnl ******************************
dnl GnomePrint checking
dnl ******************************
AC_MSG_CHECKING(for GnomePrint libraries >= 0.24)
if gnome-config --libs print > /dev/null 2>&1; then 
    vers=`gnome-config --modversion print | sed -e "s/gnome-print-//" -e 's/cvs$//' -e 's/pre$//' | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 24; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least GNOME print 0.24 for this version of Gnumeric)
    fi
else
    AC_MSG_ERROR(Did not find GnomePrint installed)
fi

dnl ******************************
dnl LibGlade checking
dnl ******************************
AC_MSG_CHECKING(for Glade libraries >= 0.14)
if gnome-config --libs libglade > /dev/null 2>&1; then 
    vers=`gnome-config --modversion libglade | awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 14; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least libglade 0.14 for this version of Gnumeric)
    fi
else
    AC_MSG_ERROR(Did not find libGlade installed)
fi

dnl ******************************
dnl Gnome App Lib checking
dnl ******************************
AC_MSG_CHECKING(for Gnome App libraries (GAL) >= 0.01)
if gnome-config --libs gal > /dev/null 2>&1; then 
    vers=`gnome-config --modversion gal | sed -e "s/gal-//" -e 's/cvs$//' -e 's/pre$//' | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 1; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least GNOME Application libs 0.01 for this version of Gnumeric)
    fi
else
    AC_MSG_ERROR(Did not find GnomeAppLib (GAL) installed)
fi

dnl ******************************
dnl gtk+ checking
dnl ******************************
AC_MSG_CHECKING(for GTK >= 1.2.7)
if gtk-config --version > /dev/null 2>&1; then 
    dnl We need the "%d" in order not to get e-notation on hpux.
    vers=`gtk-config --version | awk 'BEGIN { FS = "."; } { printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
    if test "$vers" -ge 1002007; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least GTK+ 1.2.7 for this version of Gnumeric)
    fi
else
    AC_MSG_ERROR(Did not find GTK+ installed)
fi


dnl ****************
dnl Check for pspell
dnl ****************
dnl
dnl try_pspell=true
dnl have_pspell=false
dnl pspell_msg=no
PSPELL_LIBS=""
dnl 
dnl AC_ARG_WITH(pspell,
dnl 	[--{with,without}-pspell    Compile with Pspell support or without it],
dnl 	if test x$withval = xno; then
dnl 		try_pspell=false
dnl 	fi
dnl 	if test x$withval = xyes; then
dnl 		try_pspell=true
dnl 	fi
dnl )
dnl 
dnl if $try_pspell; then
dnl 	AC_PATH_PROG(PSPELL_CONFIG,pspell-config,no)
dnl 	AC_MSG_CHECKING([for pspell])
dnl 	if test "$PSPELL_CONFIG" = "no"; then
dnl 		AC_MSG_RESULT(not found)
dnl 	else
dnl 		pspell_ver=`$PSPELL_CONFIG version 2>/dev/null`
dnl 		AC_MSG_RESULT([found ($pspell_ver)])
dnl 
dnl 		AC_CHECK_LIB(pspell, new_pspell_config, have_pspell=true)
dnl 		if test x$have_pspell = xtrue; then
dnl 			AC_DEFINE(HAVE_PSPELL)
dnl 			PSPELL_LIBS="-lpspell"
dnl 			pspell_msg=yes
dnl 		fi
dnl 	fi
dnl fi
AC_SUBST(PSPELL_LIBS)
dnl AM_CONDITIONAL(HAVE_PSPELL, $have_pspell)

dnl ******************************
dnl Check for Applix SHELF
dnl ******************************
dnl try_applix_shelf=false
dnl applix_shelf=
dnl applix_shelf_msg=no
dnl have_applix_shelf=false
dnl AC_ARG_WITH(applix_shelf,
dnl	[--{with,without}-shelf   Compile with Applix SHELF support or without it],
dnl	if test x$withval = xyes; then
dnl		try_applix_shelf=true
dnl	fi
dnl)
dnl ***************************************
dnl Check for gnome-libs which supports wm icons
dnl ***************************************
CFLAGS_save=$CFLAGS
LIBS_save=$LIBS
CFLAGS=`gnome-config --cflags gnomeui`
LIBS=`gnome-config --libs gnomeui`
use_wm_icons=false
AC_CHECK_LIB(gnomeui, gnome_window_icon_set_default_from_file, use_wm_icons=true)
if test x$use_wm_icons = xtrue; then
	AC_DEFINE(USE_WM_ICONS)
fi
CFLAGS=$CFLAGS_save
LIBS=$LIBS_save

dnl ***************************************

dnl IDL flags for non-standard prefixes
if $have_bonobo; then
	IDL_FLAGS="-I$datadir/idl"
	for path in `echo $GNOME_PATH | awk 'BEGIN { RS = ":"; } { print;}'`; do
		IDL_FLAGS="$IDL_FLAGS -I$path/share/idl"
	done
fi
AC_SUBST(IDL_FLAGS)

EXTRA_GNOME_LIBS=`gnome-config --libs gnomeui print libglade $bonobo libole2 gal`
EXTRA_GNOME_CFLAGS=`gnome-config --cflags gnomeui print libglade $bonobo libole2 gal`
AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(EXTRA_GNOME_CFLAGS)

AM_CONDITIONAL(LIBOLE2_PUBLIC_LIBRARY, false)

AM_CONDITIONAL(WITH_GDA, false)
AM_CONDITIONAL(WITH_PLAN_PERFECT, false)

AC_OUTPUT([
gnumeric.spec
Makefile
icons/Makefile
idl/Makefile
src/Makefile
src/dialogs/Makefile
src/widgets/Makefile
src/functions/Makefile
src/portability.h
doc/Makefile
doc/C/Makefile
doc/es/Makefile
corba-test/Makefile
plugins/Makefile
plugins/numtheory/Makefile
plugins/sc/Makefile
plugins/sylk/Makefile
plugins/excel/Makefile
plugins/gb/Makefile
plugins/lotus-123/Makefile
plugins/oleo/Makefile
plugins/python/Makefile
plugins/perl/Makefile
plugins/perl/ext/Makefile.PL
plugins/guile/Makefile
plugins/xbase/Makefile
plugins/html/Makefile
plugins/dif/Makefile
plugins/xml2/Makefile
plugins/applix/Makefile
plugins/plan-perfect/Makefile
plugins/gda/Makefile
intl/Makefile
po/Makefile.in
templates/Makefile
templates/english/Makefile
templates/autoformat/Makefile
templates/autoformat/General/Makefile
templates/autoformat/3D/Makefile
templates/autoformat/Classical/Makefile
templates/autoformat/Colourful/Makefile
templates/autoformat/Financial/Makefile
templates/autoformat/List/Makefile
macros/Makefile
gnumeric.desktop
stamp.h
],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])


echo "

Configuration:

	Source code location:	${srcdir}
	Compiler:		${CC} 
	
	GB Support:		${gb_msg}
	Bonobo Support:		${bonobo_msg}
"
if $have_bonobo; then
	echo "
	The Bonobo version of gnumeric is not supported,
this code is changing extremely rapidly. For any given
release it may not compile or depend on unreleased packages.

	Please do not file bonobo related bug reports.
"
fi
