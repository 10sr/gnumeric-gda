AC_INIT(src/gnumeric.h)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnumeric,0.2)
AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT
AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_PROG_YACC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL

## this should come after `AC_PROG_CC'
GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK

dnl
dnl Check for ORBit
dnl
AC_CHECK_PROG(orbit_val, orbit-config, true, false)

AM_CONDITIONAL(WITH_ORBIT, $orbit_val)

if $orbit_val; then
    AC_DEFINE(HAVE_ORBIT)
fi

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl ## internationalization support
ALL_LINGUAS="es fr ko pt it hu"
AM_GNU_GETTEXT
AC_LINK_FILES($nls_cv_header_libgt, $nls_cv_header_intl)

dnl
dnl Check for Python
dnl
AC_CHECK_PROG(python_val, python, true, false)
if $python_val; then
	PYTHON_PREFIX=`python -c 'import sys ; print sys.prefix'`
	PYTHON_EXEC_PREFIX=`python -c 'import sys ; print sys.exec_prefix'`
	changequote(<<, >>)dnl
	PYTHON_VERSION=`python -c 'import sys ; print sys.version[0:3]'`
	changequote([, ])dnl
	if test -f $PYTHON_PREFIX/include/python$PYTHON_VERSION/Python.h; then
		PYTHON_LIBS="python$PYTHON_VERSION"
		PYTHON_LIB_LOC="-L$PYTHON_EXEC_PREFIX/lib/python$PYTHON_VERSION/config"
		PYTHON_CFLAGS="-I$PYTHON_PREFIX/include/python$PYTHON_VERSION"
		AC_SUBST(PYTHON_LIBS)
		AC_SUBST(PYTHON_LIB_LOC)
		AC_SUBST(PYTHON_CFLAGS)
	else
		python_val=false
	fi
fi
AM_CONDITIONAL(WITH_PYTHON, $python_val)

AC_OUTPUT([
Makefile
src/Makefile
plugins/Makefile
plugins/sample/Makefile
plugins/python/Makefile
intl/Makefile
po/Makefile.in
macros/Makefile
stamp.h],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])


