#!/usr/bin/perl -w
# -----------------------------------------------------------------------------

my @items;
my $item = '';

while (<STDIN>) {
    # "x" out version numbers.
    if (m|^\s*<gnm:Version\s+Epoch="\d+"\s+Major="\d+"\s+Minor="\d+"\s+Full="[.0-9]+"/>\s*$|) {
	s/="[.0-9]+"/="x"/g;
    }

    if (m|^\s*<gnm:PrintInformation>\s*$| .. m|^\s*</gnm:PrintInformation>\s*$|) {
	if (m|^\s*<gnm:Margins>\s*$| .. m|^\s*</gnm:Margins>\s*$|) {
	    s/="[-.0-9a-zA-Z]+"/="x"/g;
	}
    }

    # Sort names.
    if (m|^\s*<gnm:Sheets>\s*$| .. m|^\s*</gnm:Sheets>\s*$|) {
	if (m|^\s*<gnm:Names>\s*$| .. m|^\s*</gnm:Names>\s*$|) {
	    if (m|^\s*<gnm:Names>\s*$|) {
		# Zip.
	    } elsif (m|^\s*</gnm:Names>\s*$|) {
		print sort @items;
		@items = ();
	    } else {
		$item .= $_;

		if (m|^\s*</gnm:Name>\s*$|) {
		    push @items, $item;
		    $item = '';
		}
		next;
	    }
	}
    }

    print;
}
